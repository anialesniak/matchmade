/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a commented-out sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.3.1/userguide/tutorial_java_projects.html
 */

apply plugin: 'java'

// add new source set, so we have no [main, test, integration-test]
sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

// integration tests configuration uses test configuration (especially dependencies)
configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

// creating runnable task - can be fired with `gradle clean integrationTest`
task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

// print html reports to different folders not to be unit and integration tests mixed up
tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

repositories {
    jcenter()
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'
    compile group: 'log4j', name: 'log4j', version: '1.2.17'

    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.9.1'
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.7.v20170914'
    compile group: 'com.google.guava', name: 'guava', version: '23.4-jre'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.2'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.2'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.9.2'
    compile group: 'org.yaml', name: 'snakeyaml', version: '1.19'
    compile group: 'com.github.java-json-tools', name: 'json-schema-validator', version: '2.2.8'
    compile files('lib/javaml-0.1.7.jar')
    compile 'com.google.inject:guice:4.1.0'
    compile 'com.squareup.okhttp3:okhttp:3.9.1'

    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
}

jar {
    manifest {
        attributes 'Main-Class': 'Application'
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}